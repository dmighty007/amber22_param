#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

../../check_Q-Chem.x
if( $status > 0) then
  exit(0)
endif

set sander = "${AMBERHOME}/bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

if( $?DO_PARALLEL ) then
   echo "Running $TESTsander in parallel on "`$DO_PARALLEL echo | wc -l`" cores."
else set DO_PARALLEL = ''
endif

# check on how many CPU core Q-Chem shall be running
if ( $?QC_NCPUS) then
    if ( $QC_NCPUS > 1 ) then
        echo "Q-Chem will run on $QC_NCPUS CPU cores"
    endif
else
    set QC_NCPUS = 1
    echo "Q-Chem will run on 1 CPU core"
    echo "(environment variable QC_NCPUS not set)"
endif

cat > mdin <<EOF
NMA in 15 Angstrom water shell QM/MM HF/STO-3G/SPC(Fw) NVE 2 steps at dt=0.5fs
 &cntrl
  imin     = 0,      ! do MD (no minimization)
  irest    = 1,      ! restart trajectory
  ntx      = 5,      ! read coordinates and velocities
  dt       = 0.0005  ! 0.5fs time step
  cut      = 999.9,  ! non-bonded cutoff
  ntb      = 0,      ! periodic boundary conditions
  ntpr     = 1,      ! print every step)
  ntwx     = 1,      ! write coordinates every step
  ntwr     = 2,      ! write restart file at last step
  nstlim   = 2,      ! runfor 2 steps
  nscm     = 0,      ! do not remove COM motion
  jfastw   = 4,      ! do not use routines for fast triangulated water
  ioutfm   = 1,      ! NetCDF MDCRD.
  ntxo     = 1,      ! Formatted ASCII restart file
  ifqnt    = 1,      ! do QMMM simulation
 /
 &qmmm
  qmmask    = ':1-2',
  qm_theory = 'EXTERN',
 /
 &qc
  basis    = 'sto-3g',
  method   = 'hf',
  num_threads = $QC_NCPUS,
 /
EOF

set output = nma-spcfw-15.hf_sto-3g.mdout
set restrt = nma-spcfw-15.hf_sto-3g.restrt
rm -f $output

touch dummy
$DO_PARALLEL $sander -O -p nma-spcfw-15.prmtop -c nma-spcfw-15.restrt -o $output -r $restrt < dummy > qc_job_env.dat || goto error

# remove info about number of threads from output as this may vary
grep -v 'num_threads' $output > tmp
/bin/mv tmp $output

../../dacdif -t 1 $output.save $output
../../dacdif -a 0.00001 $restrt.save $restrt

/bin/rm -rf qc_job* old.qc_job*
/bin/rm -f dummy mdin mdinfo mdcrd dummy restrt efield.dat old.efield.dat pathtable TMP qc_job_env.dat
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)









