#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif


cat > mdin <<EOF
H mass repartition
 &cntrl
   imin = 0, nstlim = 20, dt = 0.004,
   ntx = 5, tempi = 300.0, temp0 = 300.0,
   ntt = 1, tol = 0.00001, 
   ntc = 2, ntf = 2, ntb = 1, 
   ntwx =  10000, ntwe = 0, ntwr = 1000000, ntpr = 2, 
   cut = 8.0, ig = 10345,
   nscm = 2500,
   irest = 1, 
   ioutfm=1, iwrap=1, ig=71277,
 /
 &ewald
  netfrc=0,
 /
EOF

set output = mdout

set output_save = $output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -AllowSmallBox -o $output <dummy || goto error
#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../dacdif -r 1.0e-7 $output_save $output
else
  if ( $?DO_PARALLEL ) then
    #Use slightly less precision for parallel runs.
    #3 sig figs
    #Special case for 4096wat since it has one value with
    #a big relative error - so you get a 1 line failure.
    ../../dacdif -r 1.0e-2 $output_save $output
  else 
    #5 sig figs
    ../../dacdif -r 1.0e-5 $output_save $output
  endif
endif

/bin/rm -f mdin logfile mdinfo dummy mdcrd restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)









