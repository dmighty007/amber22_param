! This program tests the Fortran 90 sander interface to compute forces and
! energies

subroutine compare(computed, regression, failed, desc)
    
    implicit none

    double precision, intent(in) :: computed
    double precision, intent(in) :: regression
    character(len=*), intent(in) :: desc
    logical, intent(in out) :: failed

    ! Compare to 4 decimal places
    if (abs(computed - regression) .gt. 2.0d-4) then
        write(6, '(a,2(a,f15.4))') &
                desc, ' failed: Expected ', regression, ' got ', computed
        failed = .true.
    end if

end subroutine compare

subroutine check_for_slko(failed)

    implicit none

    logical, intent(out) :: failed
    
    integer :: ierror

    ! Try to open a file that would need to be present. If it's not present,
    ! set failed to .true. to let the caller know that the SLKO files could not
    ! be found

    open(unit=10, file="../../dat/slko/mio-1-1/C-C.skf", status='OLD', iostat=ierror)

    failed = ierror .ne. 0

    if (.not. failed) close(10)
    
    return

end subroutine check_for_slko

program test_program

    use sander_api, only: sander_input, gas_sander_input, pme_sander_input, &
                          sander_setup, potential_energy_rec, energy_forces, &
                          sander_cleanup, qmmm_input_options, qm_sander_input, &
                          sander_natom, read_inpcrd_file, get_inpcrd_natom, &
                          set_positions, set_box, is_setup, get_positions, &
                          get_box

    implicit none

    double precision, allocatable, dimension(:) :: forces 
    double precision, allocatable, dimension(:) :: coordinates
    double precision, dimension(6)              :: box
    type(potential_energy_rec) :: energies
    type(sander_input)         :: options
    type(qmmm_input_options)   :: qmmm_options
    integer                    :: alloc_failed
    integer                    :: ierr
    integer                    :: natom
    integer                    :: i

    logical :: failed = .false.

    call gas_sander_input(options, 7)
    options%cut = 9999.d0
    options%saltcon = 0.2d0
    options%gbsa = 1
    call get_inpcrd_natom("../gb7_trx/trxox.2.4ns.x", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../gb7_trx/trxox.2.4ns.x", coordinates, box, ierr)

    write(6, '(a)') &
        'Testing proper treatment of a bad prmtop; an error message is expected:'
    call sander_setup("test.parm7", coordinates, box, options, ierr=ierr)
    failed = ierr == 0 .or. is_setup()
    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
        write(6, '(62("="))')
        stop 1
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    write(6, '(a)', advance='no') 'Testing proper treatment of bad input:'
    options%cut = -1.0
    call sander_setup("../gb7_trx/prmtop_an", coordinates, box, options, ierr=ierr)

    failed = ierr == 0 .or. is_setup()
    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
        write(6, '(62("="))')
        stop 1
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    write(6, '(a)') 'Testing GB sander interface'

    options%cut = 9999.d0
    call sander_setup("../gb7_trx/prmtop_an", coordinates, box, options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0,*) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    ! Compare the energies to the output from the relevant test:
! BOND    =      631.8993  ANGLE   =      898.2543  DIHED      =      566.4453
! VDWAALS =     -768.3629  EEL     =    -7874.4913  EGB        =    -1943.0838
! 1-4 VDW =      348.8246  1-4 EEL =     5980.5047  RESTRAINT  =        0.0000
! ESURF   =       33.8338
    call compare(energies%bond, 631.8993d0, failed, 'Bond')
    call compare(energies%angle, 898.2543d0, failed, 'Angle')
    call compare(energies%dihedral, 566.4453d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 348.8246d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 5980.5047d0, failed, '1-4 Elec')
    call compare(energies%vdw, -768.3629d0, failed, 'van der Waals')
    call compare(energies%elec, -7874.4913d0, failed, 'Electrostatic')
    call compare(energies%gb, -1943.0838d0, failed, 'EGB')
    call compare(energies%surf, 33.8338d0, failed, 'SASA (GBSA)')

    call sander_cleanup

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    ! Now try explicit solvent with PME

    write(6, '(a)') 'Testing PME sander interface'
    failed = .false.
    call pme_sander_input(options)
    options%cut = 8.d0

    deallocate(forces, coordinates)

    call get_inpcrd_natom("../4096wat/eq1.x", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../4096wat/eq1.x", coordinates, box, ierr)
    call sander_setup("../4096wat/prmtop", coordinates, box, options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0, *) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    ! Compare the energies to the output from the relevant test:
! NSTEP =        1   TIME(PS) =       1.001  TEMP(K) =   298.28  PRESS =     0.0
! Etot   =    -32059.8471  EKtot   =      7282.8008  EPtot      =    -39342.6479
! BOND   =         0.0000  ANGLE   =         0.0000  DIHED      =         0.0000
! 1-4 NB =         0.0000  1-4 EEL =         0.0000  VDWAALS    =      6028.9517
! EELEC  =    -45371.5995  EHBOND  =         0.0000  RESTRAINT  =         0.0000
    call compare(energies%bond, 0.d0, failed, 'Bond')
    call compare(energies%angle, 0.d0, failed, 'Angle')
    call compare(energies%dihedral, 0.d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 0.d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 0.d0, failed, '1-4 Elec')
    call compare(energies%vdw, 6028.9517d0, failed, 'van der Waals')
    call compare(energies%elec, -45371.5995d0, failed, 'Electrostatic')

    deallocate(forces, coordinates)
    call sander_cleanup

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    ! Now test the various QM/MM capabilities

    failed = .false.
    write(6, '(a)') 'Testing the QM/MM non-periodic interface'

    call gas_sander_input(options, 1)
    options%cut = 99.d0
    options%ifqnt = 1

    call qm_sander_input(qmmm_options)
    qmmm_options%iqmatoms(1:3) = (/ 8, 9, 10 /)
    qmmm_options%qm_theory = 'PM3'
    qmmm_options%qmcharge = 0
    qmmm_options%qmgb = 2
    qmmm_options%adjust_q = 0

    call get_inpcrd_natom("../qmmm2/lysine_PM3_qmgb2/lysine.crd", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../qmmm2/lysine_PM3_qmgb2/lysine.crd", coordinates, &
                          box, ierr)
    call sander_setup("../qmmm2/lysine_PM3_qmgb2/prmtop", &
                      coordinates, box, options, qmmm_options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0, *) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    call compare(energies%bond, 0.0016d0, failed, 'Bond')
    call compare(energies%angle, 0.3736d0, failed, 'Angle')
    call compare(energies%dihedral, 0.0026d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 3.7051d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 65.9137d0, failed, '1-4 Elec')
    call compare(energies%vdw, 0.1908d0, failed, 'van der Waals')
    call compare(energies%elec, -4.1241d0, failed, 'Electrostatic')
    call compare(energies%gb, -80.1406d0, failed, 'EGB')
    call compare(energies%scf, -11.9100d0, failed, 'QM Escf')

    deallocate(forces, coordinates)
    call sander_cleanup

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    failed = .false.
    write(6, '(a)') 'Testing the QM/MM periodic interface (PM3-PDDG)'

    call pme_sander_input(options)
    options%cut = 8.d0
    options%ifqnt = 1
    options%jfastw = 4

    call qm_sander_input(qmmm_options)
    qmmm_options%qmmask = ':1-2'
    qmmm_options%qm_theory = 'PDDG-PM3'
    qmmm_options%qmcharge = 0
    qmmm_options%scfconv = 1.d-10
    qmmm_options%tight_p_conv = 1
    qmmm_options%qmmm_int = 5 ! mechanical embedding

    call get_inpcrd_natom("../qmmm2/MechEm_nma-spcfwbox/inpcrd", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../qmmm2/MechEm_nma-spcfwbox/inpcrd", coordinates, &
                          box, ierr)
    call sander_setup("../qmmm2/MechEm_nma-spcfwbox/prmtop", &
                      coordinates, box, options, qmmm_options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0, *) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    call compare(energies%bond, 605.7349d0, failed, 'Bond')
    call compare(energies%angle, 331.7679d0, failed, 'Angle')
    call compare(energies%dihedral, 0.0000d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 0.0000d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 0.0000d0, failed, '1-4 Elec')
    call compare(energies%vdw, 1281.8450d0, failed, 'van der Waals')
    call compare(energies%elec, -7409.7167d0, failed, 'Electrostatic')
    call compare(energies%scf, -37.1277d0, failed, 'QM Escf')

    ! Don't deallocate forces, since we want to change the Hamiltonian slightly
    ! but keep the same system
    call sander_cleanup

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    failed = .false.
    write(6, '(a)') 'Testing the QM/MM periodic interface (DFTB)'
    call check_for_slko(failed)

    if (.not. failed) then
        qmmm_options%qm_theory = 'DFTB'
        call sander_setup("../qmmm2/MechEm_nma-spcfwbox/prmtop", &
                          coordinates, box, options, qmmm_options, ierr=ierr)
        call energy_forces(energies, forces)
        call compare(energies%bond, 605.7349d0, failed, 'Bond')
        call compare(energies%angle, 331.7679d0, failed, 'Angle')
        call compare(energies%dihedral, 0.0000d0, failed, 'Dihedral')
        call compare(energies%vdw_14, 0.0000d0, failed, '1-4 vdW')
        call compare(energies%elec_14, 0.0000d0, failed, '1-4 Elec')
        call compare(energies%vdw, 1281.8450d0, failed, 'van der Waals')
        call compare(energies%elec, -7409.7167d0, failed, 'Electrostatic')
        call compare(energies%scf, -1209.0254d0, failed, 'QM Escf')

        if (failed) then
            write(6, '(a)') 'Possible FAILURE'
        else
            write(6, '(a)') 'PASSED'
        end if

        call sander_cleanup
    else
        write(6, '(a)') 'Could not find the SLKO files. Skipping this test.'
        failed = .false.
    end if
    write(6, '(62("="))')
    deallocate(forces, coordinates)

    failed = .false.

    ! Test various functions of the API now

    write(6, '(a)') 'Testing the broader API functionality'
    call gas_sander_input(options, 7)
    options%cut = 9999.d0
    options%saltcon = 0.2d0
    options%gbsa = 1

    call get_inpcrd_natom("../gb7_trx/trxox.2.4ns.x", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../gb7_trx/trxox.2.4ns.x", coordinates, box, ierr)
    call sander_setup("../gb7_trx/prmtop_an", coordinates, box, options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0,*) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    ! Compare the energies to the output from the relevant test:
! BOND    =      631.8993  ANGLE   =      898.2543  DIHED      =      566.4453
! VDWAALS =     -768.3629  EEL     =    -7874.4913  EGB        =    -1943.0838
! 1-4 VDW =      348.8246  1-4 EEL =     5980.5047  RESTRAINT  =        0.0000
! ESURF   =       33.8338
    call compare(energies%bond, 631.8993d0, failed, 'Bond')
    call compare(energies%angle, 898.2543d0, failed, 'Angle')
    call compare(energies%dihedral, 566.4453d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 348.8246d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 5980.5047d0, failed, '1-4 Elec')
    call compare(energies%vdw, -768.3629d0, failed, 'van der Waals')
    call compare(energies%elec, -7874.4913d0, failed, 'Electrostatic')
    call compare(energies%gb, -1943.0838d0, failed, 'EGB')
    call compare(energies%surf, 33.8338d0, failed, 'SASA (GBSA)')

    ! Now try to get the coordinates
    call get_inpcrd_natom("../gb7_trx/trxox.2.4pns.x", natom)
    if (natom /= 1654) then
        failed = .true.
    else
        call read_inpcrd_file("../gb7_trx/trxox.2.4pns.x",coordinates,box,ierr)
        call compare(coordinates(   1), -12.6153818d0, failed, 'Coordinate')
        call compare(coordinates(   2),   7.5431308d0, failed, 'Coordinate')
        call compare(coordinates(   3),  -3.4102499d0, failed, 'Coordinate')
        call compare(coordinates(   4), -12.7604696d0, failed, 'Coordinate')
        call compare(coordinates(   5),   7.4844904d0, failed, 'Coordinate')
        call compare(coordinates(   6),  -4.4080529d0, failed, 'Coordinate')
        call compare(coordinates(4960),  -0.3058399d0, failed, 'Coordinate')
        call compare(coordinates(4961), -15.0993313d0, failed, 'Coordinate')
        call compare(coordinates(4962),  13.7839947d0, failed, 'Coordinate')
        call compare(box(1), 0.d0, failed, 'box')
        call compare(box(2), 0.d0, failed, 'box')
        call compare(box(3), 0.d0, failed, 'box')
        call compare(box(4), 0.d0, failed, 'box')
        call compare(box(5), 0.d0, failed, 'box')
        call compare(box(6), 0.d0, failed, 'box')

        call set_positions(coordinates)
        call energy_forces(energies, forces)

        call compare(energies%bond, 342.0589d0, failed, 'Bond')
        call compare(energies%angle, 877.9924d0, failed, 'Angle')
        call compare(energies%dihedral, 580.6551d0, failed, 'Dihedral')
        call compare(energies%vdw_14, 357.5908d0, failed, '1-4 vdW')
        call compare(energies%elec_14, 5973.9713d0, failed, '1-4 Elec')
        call compare(energies%vdw, -770.7973d0, failed, 'van der Waals')
        call compare(energies%elec, -7883.3799d0, failed, 'Electrostatic')
        call compare(energies%gb, -1938.5736d0, failed, 'EGB')
        call compare(energies%surf, 33.8944d0, failed, 'SASA (GBSA)')
    end if

    call sander_cleanup

    call pme_sander_input(options)
    options%cut = 8.d0

    deallocate(forces, coordinates)

    call get_inpcrd_natom("../4096wat/eq1.x", natom)
    allocate(coordinates(3*natom))
    call read_inpcrd_file("../4096wat/eq1.x", coordinates, box, ierr)
    call sander_setup("../4096wat/prmtop", coordinates, box, options, ierr=ierr)
    call sander_natom(natom)
    allocate(forces(3*natom), stat=alloc_failed)
    if (alloc_failed .ne. 0) then
        write(0, *) 'Failed allocating force array'
        stop 1
    end if

    call energy_forces(energies, forces)

    ! Compare the energies to the output from the relevant test:
! NSTEP =        1   TIME(PS) =       1.001  TEMP(K) =   298.28  PRESS =     0.0
! Etot   =    -32059.8471  EKtot   =      7282.8008  EPtot      =    -39342.6479
! BOND   =         0.0000  ANGLE   =         0.0000  DIHED      =         0.0000
! 1-4 NB =         0.0000  1-4 EEL =         0.0000  VDWAALS    =      6028.9517
! EELEC  =    -45371.5995  EHBOND  =         0.0000  RESTRAINT  =         0.0000
    call compare(energies%bond, 0.d0, failed, 'Bond')
    call compare(energies%angle, 0.d0, failed, 'Angle')
    call compare(energies%dihedral, 0.d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 0.d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 0.d0, failed, '1-4 Elec')
    call compare(energies%vdw, 6028.9517d0, failed, 'van der Waals')
    call compare(energies%elec, -45371.5995d0, failed, 'Electrostatic')

    ! Change the box and check that the new energies match what is expected

    call set_box(51.d0, 51.d0, 51.d0, 90.d0, 90.d0, 90.d0)
    call energy_forces(energies, forces)

    call compare(energies%bond, 0.d0, failed, 'Bond')
    call compare(energies%angle, 0.d0, failed, 'Angle')
    call compare(energies%dihedral, 0.d0, failed, 'Dihedral')
    call compare(energies%vdw_14, 0.d0, failed, '1-4 vdW')
    call compare(energies%elec_14, 0.d0, failed, '1-4 Elec')
    call compare(energies%vdw, 12567.4552d0, failed, 'van der Waals')
    call compare(energies%elec, -42333.7294d0, failed, 'Electrostatic')

    ! Check get_box now
    call get_box(box(1), box(2), box(3), box(4), box(5), box(6))

    call compare(box(1), 51.d0, failed, 'a-vector length')
    call compare(box(2), 51.d0, failed, 'b-vector length')
    call compare(box(3), 51.d0, failed, 'c-vector length')
    call compare(box(4), 90.d0, failed, 'alpha angle')
    call compare(box(5), 90.d0, failed, 'beta angle')
    call compare(box(6), 90.d0, failed, 'gamma angle')

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    write(6, '(a)') "Checking get_positions call"
    failed = .false.
    call get_positions(forces)
    do i = 1, natom*3
        call compare(forces(i), coordinates(i), failed, 'Coordinates')
    end do

    if (failed) then
        write(6, '(a)') 'Possible FAILURE'
    else
        write(6, '(a)') 'PASSED'
    end if
    write(6, '(62("="))')

    deallocate(coordinates)
    deallocate(forces)
    call sander_cleanup

end program test_program
