#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, advanced
#TEST-STATE   undocumented

# Makefile passes in value of EMIL
if ( "$1" == "" ) then
  echo "EMIL was not configured at build time: skipping test"
  echo 'run $AMBERHOME/configure for more information'
  exit(0)
endif

if( ! $?DO_PARALLEL ) then
	echo "DO_PARALLEL not set... testing emil in serial." 
        setenv DO_PARALLEL " "
	if( ! $?TESTsander ) set TESTsander = "${AMBERHOME}/bin/sander"
else
	echo "DO_PARALLEL is set: $DO_PARALLEL"
	if( ! $?TESTsander ) set TESTsander = "${AMBERHOME}/bin/sander.MPI"
endif

cat > mdin <<EOF
  Input file for emil run, detailed emil params are in emilParameters.in 
 &cntrl
  iwrap  = 0,
  ntx = 5, irest = 1, 
  ntb = 0, igb = 1,
  cut = 12,
  ntr = 0, 
  ntc = 1, 
  ntf = 1,
  tempi = 300.0, temp0 = 300.0,
  ntt = 3, gamma_ln=1.0,
  nstlim = 100,
  dt = 0.001,
  ntpr = 10, ntwx = 5000, ntwr = 5000,
  ntave = 100000,
  ig = 1902,
  ntr = 0, vlimit = 0,
  emil_do_calc = 1
 /
 &emil_cntrl
  emil_paramfile = "emilParameters.in", 
  emil_logfile   = "emil.log", 
  emil_model_infile  = "wellsIn.dat",
  emil_model_outfile = "wells.dat",
 /

EOF

echo "#Run        CPU(s)  Ref.CPU(s) Wall(s) Ref.Wall(s)" > test_timings.dat
foreach lambda (0.0 0.1 0.9)
#foreach lambda (0.5)

##generate input file for emil run
cat > emilParameters.in <<EOF

##well description
epsilonWell     20.0
rWell            1.0 

##define molecules in each phase
##... solid only for this run.
solidRes   ACE,CYS,SER,ALA,LEU,ILE,THR,VAL,ASN,ASH,NME

##emil has its own RNG 
Seed             1902

##set MC move frequencies -- not applicable for GBSA
swapTriesPerChain    0.0 
relocTriesPerChain   0.0 

lambda               $lambda

EOF


set output = mdout.emil.$lambda
($DO_PARALLEL $TESTsander -O -i mdin \
                          -c csallDimer.md1 \
                          -p csallDimer.top \
                          -o $output >emil.stdout.dummy ) \
                   >& emil.stderr.$lambda || goto error


mv emil.log emil.log.$lambda


##uncomment these lines to regenerate the reference sets.
#cp emil.log.$lambda    emil.log.$lambda.save
#cp $output            $output.save

###save the timings just for a quick performance check
set t1 = `grep "Total CPU" $output       | awk '{for(i=2;i<NF;i++){if($i=="time:"){print $(i+1)}}}'`
set t2 = `grep "Total CPU" $output.save  | awk '{for(i=2;i<NF;i++){if($i=="time:"){print $(i+1)}}}'`
set w1 = `grep "Total wall" $output      | awk '{for(i=2;i<NF;i++){if($i=="time:"){print $(i+1)}}}'`
set w2 = `grep "Total wall" $output.save | awk '{for(i=2;i<NF;i++){if($i=="time:"){print $(i+1)}}}'`

echo $output $t1 $t2 $w1 $w2>> test_timings.dat

##diff the test and reference
../../dacdif -t 2  $output.save              $output 
../../dacdif -t 2  emil.log.$lambda.save     emil.log.$lambda


end ##loop over lambda

/bin/rm -f *dummy
/bin/rm -f mdin mdinfo
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)










