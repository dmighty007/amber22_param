#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

set sander = "${AMBERHOME}/bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

if( $?DO_PARALLEL ) then
   set numprocs=`echo $DO_PARALLEL | awk -f ../../numprocs.awk`
   if( $numprocs > 1 ) then
      echo "This test is not set up for parallel"
      exit 0
   endif
else
   set DO_PARALLEL = ""
endif

cat > mdin <<EOF
2 steps NVE pure QM MD with Orca (dt=0.5fs, no shake)
 &cntrl
  imin   = 0,           !no minimization
  irest  = 1,           !restart
  ntx    = 5,           !coordinates and velocities are read in
  cut    = 9999.9       !non-bonded interactions cutoff
  dt     = 0.0005,      !0.5fs time step
  ntb    = 0,           !no periodicity and PME off!
  ntt    = 0,           !turn off thermostat
  ntpr   = 1,           !print details to log every 10 steps (every 5fs)
  ntwx   = 1,           !write coordinates to mdcrd every 10 steps (every 5fs)
  ntwr   = 2,           !write restart file at last step
  nstlim = 2,           !run for 2 steps (1 fs at dt=0.5fs)
  nscm   = 0,           !No removal of COM motion,
  ioutfm = 1,           !NetCDF MDCRD.
  ifqnt  = 1,
 /
 &qmmm
  qmmask    = '@*',
  qmcharge  = 0,
  qm_theory = 'quick',
  verbosity = 0,
 /
 &quick
  basis  = '6-31g',
  method = 'LIBXC=GGA_X_B88,GGA_C_LYP',
  debug  = 1,
 /
EOF

set output = h2o.blyp_6-31g_md.out
rm -f $output

touch dummy
$DO_PARALLEL $sander -O -p h2o.prmtop -c h2o.restrt -o $output < dummy || goto error

../../dacdif -a 5.0e-02 $output.save $output

/bin/rm -f dummy mdin mdinfo mdcrd dummy restrt quick.dat quick.out
exit(0)

error:
if ( `tail $output | grep "Code was compiled without QUICK support" | wc -l` > 0 ) then
  echo 'Quick support not installed - Skipping Test...'
  echo ''
  exit(0)
else
  echo "  ${0}:  Program error"
  exit(1)
endif
