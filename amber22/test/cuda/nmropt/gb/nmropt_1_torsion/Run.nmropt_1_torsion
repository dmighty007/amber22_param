#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP Test dihedral restraints using nmropt = 1
#TEST-PURPOSE regression basic pedagogical
#TEST-STATE   ok

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
Test of angle restraints using nmropt=1
 &cntrl                                                                        
   nstlim=20,
   ntpr=1, ntt=1,
   dt=0.001,
   igb=5, 
   nmropt=1,
   ig=71277,
   cut=99999,
 /
 &wt type='DUMPFREQ', istep1=2  /
 &wt type='END'   /
DISANG=torsion_gb.RST
DUMPAVE=torsion_gb.dat
LISTIN=POUT
LISTOUT=POUT
/
EOF

cat > torsion_gb.RST << EOF
#  torsion restraint for residue 34
 &rst  iat=720,719,710,711 r1=-80.0, r2=20, r3=20, r4=100, rk2 = 40.0,
       rk3 = 40.0, 
 /
EOF

set output = mdout
set vs_t_output = torsion_gb.dat
set output_save = $output.GPU_$1
set vs_t_output_save = $vs_t_output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -p ../myoglobin_gb.prmtop -c ../myoglobin_gb.inpcrd -i mdin -o $output <dummy || goto error

#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../../../dacdif -r 1.0e-7 $output_save $output
  ../../../../dacdif -r 1.0e-7 $vs_t_output_save $vs_t_output
else
  #4 sig figs
  ../../../../dacdif -r 1.0e-4 $output_save $output
  ../../../../dacdif -r 1.0e-4 $vs_t_output_save $vs_t_output
endif

/bin/rm -f mdin restrt mdinfo profile_mpi logfile dummy

exit 0

error:
echo "  ${0}:  Program error"
exit(1)


