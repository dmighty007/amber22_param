#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP test steered MD with 1 distance
#TEST-PURPOSE Regression
#TEST-STATE   Documented

#$1 = PREC_MODEL
#$2 = NETCDF

#Note that the GPU mdout files may have fundamental errors when diffed with the
#CPU mdouts due to GPU not supporting Jarzynski and COM tests

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
 Test of nmropt on 1 distance
 &cntrl                                                                        
   ig=71277,       ! set random seed
   nstlim = 20,    ! 100 steps
   igb=5,          ! gb solvent
   ntb=0,          ! no periodic boundaries
   ntx=5,          ! restart with velocities
   irest=1, 
   ntpr=1,         ! mdout
   dt=0.001,       ! 1 fs timestep
   ntt=1,          ! T coupling
   ntc=2,          ! use shake
   ntf=2,  
   tol=0.000001,
   ntwr = 200,     ! dont write rst
   jar=1, 
   cut=99999,
 /
 &wt type='DUMPFREQ', istep1=2  /
 &wt type='END'   /
DISANG=dist_gb.RST
DUMPAVE=dist_gb_vs_t
LISTIN=POUT
LISTOUT=POUT
/
EOF

cat > dist_gb.RST << EOF
# test jarzyinski on a distance
 &rst  iat=1177,1217, r2=5.49, rk2 = 100.0, r2a=6.49,   /
EOF

set output = mdout
set vs_t_output = dist_gb_vs_t
set output_save = $output.GPU_$1
set vs_t_output_save = $vs_t_output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -p ../myoglobin_gb.prmtop -c ../myoglobin_gb.inpcrd -i mdin -o $output <dummy || goto error

#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../../../dacdif -r 1.0e-7 $output_save $output
  ../../../../dacdif -r 1.0e-7 $vs_t_output_save $vs_t_output
else
  #4 sig figs
  ../../../../dacdif -r 1.0e-4 $output_save $output
  ../../../../dacdif -r 1.0e-4 $vs_t_output_save $vs_t_output
endif

/bin/rm -f mdin restrt mdinfo dist_gb.RST logfile dummy

exit 0

error:
echo "  ${0}:  Program Error"
exit(1)

