#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP Run an angle restraint using nmropt = 1 in explicit solvent
#TEST-PURPOSE pedagogical regression
#TEST-STATE   ok

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
Test of angle restraints using nmropt=1 with PBC
 &cntrl
   nstlim=20,
   ntpr=1, ntt=1,
   dt=0.001,
   nmropt=1,
   ig=71277,
 /
 &ewald
  nfft1=64, nfft2=64, nfft3=64,netfrc=0,
 /
 &wt type='DUMPFREQ', istep1=2  /
 &wt type='END'   /
DISANG=angle_pbc.RST
DUMPAVE=angle_pbc_vs_t
LISTIN=POUT
LISTOUT=POUT
/
EOF

cat > angle_pbc.RST <<EOF
#  angle restraint for residue 34
 &rst  iat=542,545,548, r1=45.0, r2=90.0, r3=90.0, r4=115.0, rk2 = 10.0,
       rk3 = 15.0,   /
EOF

set output = mdout
set vs_t_output = angle_pbc_vs_t
set output_save = $output.GPU_$1
set vs_t_output_save = $vs_t_output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -p ../myoglobin_pbc.prmtop -c ../myoglobin_pbc.inpcrd -i mdin -o $output <dummy || goto error

#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../../../dacdif -r 1.0e-7 $output_save $output
  ../../../../dacdif -r 1.0e-7 $vs_t_output_save $vs_t_output
else
  #4 sig figs
  ../../../../dacdif -r 1.0e-4 $output_save $output
  ../../../../dacdif -r 1.0e-4 $vs_t_output_save $vs_t_output
endif


/bin/rm -f mdin restrt mdinfo profile_mpi logfile angle_pbc.RST dummy

exit(0)

error:
echo "  ${0}:  Program error"
exit(1)


