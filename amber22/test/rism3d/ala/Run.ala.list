#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP Test the 3D-RISM closure list interface and execution
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

if( ! $?TESTsander ) set TESTsander = "${AMBERHOME}/bin/sander"

if ! { ../checkrismunsupported.sh $TESTsander } exit 0

if( ! $?DO_PARALLEL ) then
        setenv DO_PARALLEL " "
else
        set numprocs=`echo $DO_PARALLEL | awk -f ../../numprocs.awk `
        if ( "$numprocs" > 2 ) then
            echo " DO_PARALLEL set to $DO_PARALLEL"
            goto toomany
        else if ( $?MP_PROCS)then
            if ($MP_PROCS > 3)then
#               --- For IBM AIX ---
                echo "    MP_PROCS set to "$MP_PROCS
                goto toomany
            endif
        endif
endif

cat > mdin <<EOF
Alanine dipeptide run
 &cntrl
    ntx=1, ntpr=1, ntwx=0,ntwr=0
    nstlim=2,dt=0.001,
    ntt=0, ig=314159, tempi=0,
    ntp=0,
    ntc=1,ntf=1,
    ntb=0,
    nrespa=1,
    irism=1
    cut=999,
 /
&rism
    closure="kh","pse2","pse3","hnc"
    tolerance=1e-2,1e-5
    solvbox=32d0,32d0,32d0
    ng3=16,16,16
    buffer=-1
    verbose=1
    apply_rism_force=0
    mdiis_del=0.5d0
    gfCorrection = 1
    pcplusCorrection = 1
    uccoeff=-0.11869162,-0.220803,-0.000506781,0.0096521
    treeDCF=.false.
    treeTCF=.false.
    treeCoulomb=.false.
    asympKSpaceTolerance = 0
    ljTolerance = 0

/
EOF

$DO_PARALLEL $TESTsander -O -i mdin -o ala.list.out \
   -p ala.parm7 -c ala.rst7 -xvv ../../../AmberTools/test/rism1d/spc-kh/spc.xvv.save \
   || goto error

../../../AmberTools/test/nab/checkrism3d -err 1e-6 ala.list.out.save ala.list.out
/bin/rm -f mdin mdinfo restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

toomany:
    echo " too many processors for this test, exiting"
    echo "============================================================"
    exit(0)










