#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
 check of isotropic periodic sum simulation, ntb=2 AMD=3
 &cntrl
    imin=0, ntx=7, ntpr=1, 
    nscm=5000,iwrap=1,
    ntf=2, ntc=2,
    ntb=2, ntp=1, tautp=5.0, taup=5.0,
    nstlim=15, t=0.0, dt=0.001,
    cut=10.0,
    ntt=1,
    irest=1
    ntpr=1, ntwx=1,ntwr=1,
    ips=1,
    iamd=3,
   ethreshp=-775942,alphap=4806,
   ethreshd=655,alphad=21, ig=71277,
 /
EOF

set output = mdout.ips.amd3

set output_save = $output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -i mdin -o $output <dummy || goto error
#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../../dacdif -r 1.0e-7 $output_save $output
else
  #4 sig figs
  ../../../dacdif -r 4.9e-4 $output_save $output
endif

/bin/rm -f mdin logfile mdinfo dummy mdcrd restrt amd.log
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)








