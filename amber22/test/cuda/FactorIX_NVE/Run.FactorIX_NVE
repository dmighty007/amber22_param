#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
Typical Production MD NVE with
 GOOD energy conservation.
 &cntrl
   ntx=5, irest=1,
   ntc=2, ntf=2, tol=0.000001,
   nstlim=100, 
   ntpr=5, ntwx=0,
   ntwr=100, 
   dt=0.002, cut=8.,
   ntt=0, ntb=1, ntp=0,
   ioutfm=1, ig=71277,
 /
 &ewald
  nfft1=144,nfft2=96,nfft3=96,dsum_tol=0.000001,netfrc=0,
 /
EOF

set output = FactorIX_NVE.out

set output_save = $output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -o $output -c inpcrd -r restrt -x mdcrd < dummy || goto error

#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../dacdif -r 1.0e-7 $output_save $output
else
  #5 sig figs
  ../../dacdif -r 1.0e-5 $output_save $output
endif

/bin/rm -f mdin logfile mdinfo dummy restrt mdcrd
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)










