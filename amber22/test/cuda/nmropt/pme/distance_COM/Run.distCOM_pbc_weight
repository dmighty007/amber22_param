#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP Testing nmropt restraints on 2 distances with 1 center-of-mass group
#TEST-PURPOSE pedagogical regession advanced
#TEST-STATE   ok

#$1 = PREC_MODEL
#$2 = NETCDF

#Note that the GPU mdout files may have fundamental errors when diffed with the
#CPU mdouts due to GPU not supporting Jarzynski and COM tests

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
 Test of nmropt on distance
 &cntrl                                                                        
   ig=71277,       ! set random seed
   nstlim = 20,    ! 50 steps
   igb=0,          ! explicit solvent
   ntb=1,          ! periodic boundaries
   ntx=5,          ! restart with velocities
   irest=1, 
   ntpr=1,         ! mdout
   dt=0.001,       ! 1 fs timestep
   ntt=1,          ! T coupling
   ntc=2,          ! use shake
   ntwx=0,
   ntf=2,  
   ntwr = 200,     ! dont write rst
   nmropt=1, 
 /
 &ewald
  nfft1=64,
  nfft2=64,
  nfft3=64,netfrc=0,
 /
 &wt type='DUMPFREQ', istep1=2  /
 &wt type='END'   /
DISANG=distCOM_pbc.RST
DUMPAVE=distCOM_pbc_vs_t_weight
LISTIN=POUT
LISTOUT=POUT
/
EOF

cat > distCOM_pbc.RST << EOF
# bond restraints for ex solv
 &rst  iat=1273,1233, r1=1.52, r2=6.52, r3=6.52, r4=11.52, rk2 = 20.0, rk3 = 20.0,   /
 &rst  iat=1237,-1, r1=2.72, r2=7.72, r3=7.72, r4=12.72, rk2 = 10.0, rk3 = 10.0, igr1=0,0,0,0, igr2=1171,1169,1175,0, fxyz=0,0,1, outxyz=1 /
EOF

set output = mdout_weight
set vs_t_output = distCOM_pbc_vs_t_weight
set output_save = $output.GPU_$1
set vs_t_output_save = $vs_t_output.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -p ../myoglobin_pbc.prmtop -c ../myoglobin_pbc.inpcrd -i mdin -o $output <dummy || goto error

#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
   ../../../../dacdif -r 1.0e-7 $output_save $output
   ../../../../dacdif -r 1.0e-7 $vs_t_output_save $vs_t_output
else
  #4 sig figs
   ../../../../dacdif -r 1.0e-4 $output_save $output
   ../../../../dacdif -r 1.0e-4 $vs_t_output_save $vs_t_output
endif


/bin/rm -f mdin restrt mdinfo  distCOM_pbc.RST logfile profile_mpi dummy

exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

